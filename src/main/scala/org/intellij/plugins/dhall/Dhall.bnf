{
    parserClass="org.intellij.plugins.dhall.parser.DhallParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Dhall"
    psiImplClassSuffix="Impl"
    psiPackage="org.intellij.plugins.dhall.psi"
    psiImplPackage="org.intellij.plugins.dhall.psi.impl"

    elementTypeHolderClass="org.intellij.plugins.dhall.psi.DhallTypes"
    elementTypeClass="org.intellij.plugins.dhall.psi.DhallElementType"
    tokenTypeClass="org.intellij.plugins.dhall.psi.DhallTokenType"

    tokens=[
        // stringlike entities to be manually parsed
        // 1. labels, keywords, builtins
        // 2. braced and non-braced comments
        // 3. single and double-quoted strings
        // Flyweight entities for characters
        TAB = "regexp:\x09"
        SPACE="regexp:\x20" // Space
        EXCLAMATION="!" // Exclamation mark regexp:\x21
        DOUBLE_QUOTE="regexp:\x22" // Double quotes (or speech marks)
        HASH_SIGN="#" // Number regexp:\x23
        DOLLAR="$" // Dollar regexp:\x24
        PERCENT="%" // Percent sign regexp:\x25
        AMPERSAND="&" // Ampersand regexp:\x26
        SINGLE_QUOTE="'" // Single quote regexp:\x27
        LEFT_PAREN="(" // Open parenthesis (or open bracket) regexp:\x28
        RIGHT_PAREN=")" // Close parenthesis (or close bracket) regexp:\x29
        ASTERISK="*" // Asterisk regexp:\x2A
        PLUS="+" // Plus regexp:\x2B
        COMMA="," // Comma regexp:\x2C
        MINUS="-" // Hyphen regexp:\x2D
        DOT="." // Period, dot or full stop regexp:\x2E
        SLASH="/" // Slash or divide regexp:\x2F
        ZERO="0" // Zero regexp:\x30
        ONE="1" // One regexp:\x31
        TWO="2" // Two regexp:\x32
        THREE="3" // Three regexp:\x33
        FOUR="4" // Four regexp:\x34
        FIVE="5" // Five regexp:\x35
        SIX="6" // Six regexp:\x36
        SEVEN="7" // Seven regexp:\x37
        EIGHT="8" // Eight regexp:\x38
        NINE="9" // Nine regexp:\x39
        COLON=":" // Colon regexp:\x3A
        SEMICOLON=";" // Semicolon regexp:\x3B
        LT="<" // ess than (or open angled bracket) regexp:\x3C
        EQUALS="=" // Equals regexp:\x3D
        GT=">" // Greater than (or close angled bracket) regexp:\x3E
        QUESTION="?" // Question mark. regexp:\x3F
        AT="@" // At symbol. regexp:\x40
        UA="A" // Uppercase. regexp:\x41
        UB="B" // Uppercase. regexp:\x42
        UC="C" // Uppercase. regexp:\x43
        UD="D" // Uppercase. regexp:\x44
        UE="E" // Uppercase. regexp:\x45
        UF="F" // Uppercase. regexp:\x46
//        UG="G" // Uppercase. regexp:\x47
//        UH="H" // Uppercase. regexp:\x48
        UI="I" // Uppercase. regexp:\x49
//        UJ="J" // Uppercase. regexp:\x4A
        UK="K" // Uppercase. regexp:\x4B
        UL="L" // Uppercase. regexp:\x4C
        UM="M" // Uppercase. regexp:\x4D
        UN="N" // Uppercase. regexp:\x4E
        UO="O" // Uppercase. regexp:\x4F
//        UP="P" // Uppercase. regexp:\x50
//        UQ="Q" // Uppercase. regexp:\x51
//        UR="R" // Uppercase. regexp:\x52
        US="S" // Uppercase. regexp:\x53
        UT="T" // Uppercase. regexp:\x54
//        UU="U" // Uppercase. regexp:\x55
//        UV="V" // Uppercase. regexp:\x56
//        UW="W" // Uppercase. regexp:\x57
//        UX="X" // Uppercase. regexp:\x58
//        UY="Y" // Uppercase. regexp:\x59
        UZ="Z" // Uppercase. regexp:\x5A
        UALPHA="regexp:[GHJPQRUVWXY]" // catchall for all other uppercase letters
        LEFT_SQUARE_BRACKET="[" // Opening bracket regexp:\x5B
        BACKSLASH="regexp:\x5C" // Backslash
        RIGHT_SQUARE_BRACKET="]" // Closing bracket regexp:\x5D
        CIRCUMFLEX="^" // Caret - circumflex regexp:\x5E
        UNDERSCORE="_" // Underscore regexp:\x5F
        GRAVE="regexp:\x60" // Grave accent
        a="a" // Lowercase. regexp:\x61
        b="b" // Lowercase. regexp:\x62
        c="c" // Lowercase. regexp:\x63
        d="d" // Lowercase. regexp:\x64
        e="e" // Lowercase. regexp:\x65
        f="f" // Lowercase. regexp:\x66
        g="g" // Lowercase. regexp:\x67
        h="h" // Lowercase. regexp:\x68
        i="i" // Lowercase. regexp:\x69
//        j="j" // Lowercase. regexp:\x6A
        k="k" // Lowercase. regexp:\x6B
        l="l" // Lowercase. regexp:\x6C
        m="m" // Lowercase. regexp:\x6D
        n="n" // Lowercase. regexp:\x6E
        o="o" // Lowercase. regexp:\x6F
        p="p" // Lowercase. regexp:\x70
//        q="q" // Lowercase. regexp:\x71
        r="r" // Lowercase. regexp:\x72
        s="s" // Lowercase. regexp:\x73
        t="t" // Lowercase. regexp:\x74
        u="u" // Lowercase. regexp:\x75
        v="v" // Lowercase. regexp:\x76
        w="w" // Lowercase. regexp:\x77
        x="x" // Lowercase. regexp:\x78
        y="y" // Lowercase. regexp:\x79
//        z="z" // Lowercase. regexp:\x7A
        LALPHA="regexp:[jqz]"
        LEFT_BRACE="{" // Opening brace regexp:\x7B
        PIPE="|" // Vertical bar regexp:\x7C
        RIGHT_BRACE="}" // Closing brace regexp:\x7D
        TILDE="~" // Equivalency sign - tilde regexp:\x7E
        DELETE="regexp:\x7F" // Delete - counted as a printable character here
        // These only tokenize the special characters. The definition
        // including regular characters is given as grammar rules.
        UNICODE_FORALL = "regexp:\u2200" // âˆ€
        UNICODE_COMBINE       = "regexp:\u2227" // Combine. /\
        UNICODE_COMBINE_TYPES = "regexp:\u2A53" // Combine types. //\\
        UNICODE_EQUIVALENT    = "regexp:\u2261" // Equivalent. ===
        UNICODE_PREFER        = "regexp:\u2AFD" // Prefer: //
        UNICODE_LAMBDA        = "regexp:\u03BB" // Lambda: \
        UNICODE_ARROW         = "regexp:\u2192" // Arrow: ->
        NEW_LINE =      "regexp:\x0a" // \n
        CARRIAGE_RETURN = "regexp:\x0d" // \r
        // we can encode all of valid-non-ascii in here - any other significant character
        // will be filtered out through precedence
        // valid-non-ascii =
        //       %x80-D7FF
        //     ; %xD800-DFFF = surrogate pairs
        //     / %xE000-FFFD
        //     ; %xFFFE-FFFF = non-characters
        //     / %x10000-1FFFD
        //     ; %x1FFFE-1FFFF = non-characters
        //     / %x20000-2FFFD
        //     ; %x2FFFE-2FFFF = non-characters
        //     / %x30000-3FFFD
        //     ; %x3FFFE-3FFFF = non-characters
        //     / %x40000-4FFFD
        //     ; %x4FFFE-4FFFF = non-characters
        //     / %x50000-5FFFD
        //     ; %x5FFFE-5FFFF = non-characters
        //     / %x60000-6FFFD
        //     ; %x6FFFE-6FFFF = non-characters
        //     / %x70000-7FFFD
        //     ; %x7FFFE-7FFFF = non-characters
        //     / %x80000-8FFFD
        //     ; %x8FFFE-8FFFF = non-characters
        //     / %x90000-9FFFD
        //     ; %x9FFFE-9FFFF = non-characters
        //     / %xA0000-AFFFD
        //     ; %xAFFFE-AFFFF = non-characters
        //     / %xB0000-BFFFD
        //     ; %xBFFFE-BFFFF = non-characters
        //     / %xC0000-CFFFD
        //     ; %xCFFFE-CFFFF = non-characters
        //     / %xD0000-DFFFD
        //     ; %xDFFFE-DFFFF = non-characters
        //     / %xE0000-EFFFD
        //     ; %xEFFFE-EFFFF = non-characters
        //     / %xF0000-FFFFD
        //     ; %xFFFFE-FFFFF = non-characters
        //     / %x100000-10FFFD
        //     ; %x10FFFE-10FFFF = non-characters
        VALID_NON_ASCII = "regexp:[\u0080-\uD7FF\uE000-\uFFFD\U010000-\U01FFFD\U020000-\U02FFFD\U030000-\U03FFFD\U040000-\U04FFFD\U050000-\U05FFFD\U060000-\U06FFFD\U070000-\U07FFFD\U080000-\U08FFFD\U090000-\U09FFFD\U0A0000-\U0AFFFD\U0B0000-\U0BFFFD\U0C0000-\U0CFFFD\U0D0000-\U0DFFFD\U0E0000-\U0EFFFD\U0F0000-\U0FFFFD\U100000-\U10FFFD]"
    ]
}

root ::= complete-expression

complete-expression ::= whsp expression whsp

expression ::=
    // "\(x : a) -> b"
      lambda whsp LEFT_PAREN whsp nonreserved-label whsp COLON whsp1 expression whsp RIGHT_PAREN whsp arrow whsp expression

    // "if a then b else c"
    | if whsp1 expression whsp then whsp1 expression whsp else whsp1 expression

    // "let x : t = e1 in e2"
    // "let x     = e1 in e2"
    // We allow dropping the `in` between adjacent let-expressions; the following are equivalent:
    // "let x = e1 let y = e2 in e3"
    // "let x = e1 in let y = e2 in e3"
    | let_binding let_binding* in whsp1 expression

    // "forall (x : a) -> b"
    | forall whsp LEFT_PAREN whsp nonreserved-label whsp COLON whsp1 expression whsp RIGHT_PAREN whsp arrow whsp expression

    // "a -> b"
    // NOTE: Backtrack if parsing this alternative fails
    | operator-expression whsp arrow whsp expression

    // "merge e1 e2 : t"
    // NOTE: Backtrack if parsing this alternative fails since we can't tell
    // from the keyword whether there will be a type annotation or not
    | merge-expression

    // "[] : t"
    // NOTE: Backtrack if parsing this alternative fails since we can't tell
    // from the opening bracket whether or not this will be an empty list or
    // a non-empty list
    | empty-list-literal

    // "toMap e : t"
    // NOTE: Backtrack if parsing this alternative fails since we can't tell
    // from the keyword whether there will be a type annotation or not
    | tomap-expression

    // "assert : Natural/even 1 === False"
    | assert whsp COLON whsp1 expression

    // "x : t"
    | annotated-expression

    // The first four pins here are the rules that do not need backtracking. Once those
    // keywords are matched, the rule is matched.
    { pin(".*")="forall|lambda|if|assert|let_binding"}

// These two rules are pinned at the colon, since according to the comments we must backtrack
// because we do not know when parsing from the left if there will be a type annotation. But
// once we get to the colon, we know there ought to be a type annotation.
private merge-expression ::= merge whsp1 import-expression whsp1 import-expression whsp COLON whsp1 application-primary {pin=COLON}
private tomap-expression ::= toMap whsp1 import-expression whsp COLON whsp1 application-primary {pin=COLON}

fake character ::= all_ascii_printable | VALID_NON_ASCII

simple-label ::= keyword simple-label-next-char+
               | !keyword (simple-label-first-char simple-label-next-char*)
simple-label-first-char ::= alpha | UNDERSCORE { extends = character elementType = character }
simple-label-next-char ::= alphanum | MINUS | SLASH | UNDERSCORE { extends = character elementType = character }


label ::= GRAVE quoted-label GRAVE | simple-label


//quoted-label-char =
//      %x20-5F
//        ; %x60 = '`'
//    / %x61-7E
quoted-label-char ::= pre_digit_printable
              | between_digit_upper_alpha
              | between_upper_lower_alpha_no_grave
              | alphanum { extends = character elementType = character }

quoted-label ::= quoted-label-char+

nonreserved-label ::= builtin simple-label-next-char+
                    | !builtin label

fake operator ::= combine
           | combine-types
           | equivalent
           | prefer
           | arrow
           | complete
           | question
           | double-pipe
           | plus
           | double-plus
           | hash-sign
           | double-ampersand
           | asterisk
           | double-equals
           | not-equals
           | dot-op

// Special Symbols
combine ::= UNICODE_COMBINE | SLASH BACKSLASH {extends = operator elementType = operator}
combine-types ::= UNICODE_COMBINE_TYPES | SLASH SLASH BACKSLASH BACKSLASH {extends = operator elementType = operator}
equivalent ::= UNICODE_EQUIVALENT | EQUALS EQUALS EQUALS {extends = operator elementType = operator}
prefer ::= UNICODE_PREFER | SLASH SLASH !(BACKSLASH BACKSLASH) {extends = operator elementType = operator}
lambda ::= UNICODE_LAMBDA | BACKSLASH {extends = operator}
arrow ::= UNICODE_ARROW | MINUS GT {extends = operator elementType = operator}
complete ::= COLON COLON {extends = operator elementType = operator}
question ::= QUESTION {extends = operator elementType = operator}
double-pipe ::= PIPE PIPE {extends = operator elementType = operator}
plus ::= PLUS {extends = operator elementType = operator}
double-plus ::= PLUS PLUS {extends = operator elementType = operator}
hash-sign ::= HASH_SIGN {extends = operator elementType = operator}
double-ampersand ::= AMPERSAND AMPERSAND {extends = operator elementType = operator}
asterisk ::= ASTERISK {extends = operator elementType = operator}
double-equals ::= EQUALS EQUALS {extends = operator elementType = operator}
not-equals ::= EXCLAMATION EQUALS {extends = operator elementType = operator}
dot-op ::= DOT {extends = operator}


// Keywords
keyword ::= if
          | then
          | else
          | let
          | in
          | using
          | missing
          | assert
          | as
          | infinity
          | nan
          | merge
          | some
          | toMap
          | forall

if                    ::= i f { extends = keyword }
then                  ::= t h e n { extends = keyword }
else                  ::= e l s e { extends = keyword }
let                   ::= l e t { extends = keyword }
in                    ::= i n { extends = keyword }
as                    ::= a s { extends = keyword }
using                 ::= u s i n g { extends = keyword }
merge                 ::= m e r g e { extends = keyword }
missing               ::= m i s s i n g { extends = keyword }
infinity              ::= UI n f i n i t y { extends = keyword }
nan                   ::= UN a UN { extends = keyword }
some                  ::= US o m e { extends = keyword }
toMap                 ::= t o UM a p { extends = keyword }
assert                ::= a s s e r t { extends = keyword }
forall                ::= UNICODE_FORALL | f o r a l l { extends = keyword }

// Reserved identifiers
optional ::= UO p t i o n a l {extends = builtin}
textid ::= UT e x t {extends = builtin}
list ::= UL i s t {extends = builtin}
location ::= UL o c a t i o n {extends = builtin}

// Reserved identifiers that are also builtins

builtin ::=
      natural-fold
    | natural-build
    | natural-iszero
    | natural-even
    | natural-odd
    | natural-tointeger
    | natural-show
    | integer-todouble
    | integer-show
    | integer-negate
    | integer-clamp
    | natural-subtract
    | double-show
    | list-build
    | list-fold
    | list-length
    | list-head
    | list-last
    | list-indexed
    | list-reverse
    | optional-fold
    | optional-build
    | text-show
    | bool
    | true
    | false
    | optional
    | none
    | natural
    | integer
    | double
    | textid
    | list
    | type
    | kind
    | sort { extends = identifier }

bool ::= UB o o l { extends = builtin }
true ::= UT r u e { extends = builtin }
false ::= UF a l s e { extends = builtin }
none ::= UN o n e { extends = builtin }
natural ::= UN a t u r a l { extends = builtin }
integer ::= UI n t e g e r { extends = builtin }
double ::= UD o u b l e { extends = builtin }
type ::= UT y p e { extends = builtin }
kind ::= UK i n d { extends = builtin }
sort ::= US o r t { extends = builtin }
natural-fold ::= UN a t u r a l SLASH f o l d { extends = builtin }
natural-build ::= UN a t u r a l SLASH b u i l d { extends = builtin }
natural-iszero ::= UN a t u r a l SLASH i s UZ e r o { extends = builtin }
natural-even ::= UN a t u r a l SLASH e v e n { extends = builtin }
natural-odd ::= UN a t u r a l SLASH o d d { extends = builtin }
natural-tointeger ::= UN a t u r a l SLASH t o UI n t e g e r { extends = builtin }
natural-show ::= UN a t u r a l SLASH s h o w { extends = builtin }
natural-subtract ::= UN a t u r a l SLASH s u b t r a c t { extends = builtin }
integer-todouble ::= UI n t e g e r SLASH t o UD o u b l e { extends = builtin }
integer-show ::= UI n t e g e r SLASH s h o w { extends = builtin }
integer-negate ::= UI n t e g e r SLASH n e g a t e { extends = builtin }
integer-clamp ::= UI n t e g e r SLASH c l a m p { extends = builtin }
double-show ::= UD o u b l e SLASH s h o w { extends = builtin }
list-build ::= UL i s t SLASH b u i l d { extends = builtin }
list-fold ::= UL i s t SLASH f o l d { extends = builtin }
list-length ::= UL i s t SLASH l e n g t h { extends = builtin }
list-head ::= UL i s t SLASH h e a d { extends = builtin }
list-last ::= UL i s t SLASH l a s t { extends = builtin }
list-indexed ::= UL i s t SLASH i n d e x e d { extends = builtin }
list-reverse ::= UL i s t SLASH r e v e r s e { extends = builtin }
optional-fold ::= UO p t i o n a l SLASH f o l d { extends = builtin }
optional-build ::= UO p t i o n a l SLASH b u i l d { extends = builtin }
text-show ::= UT e x t SLASH s h o w { extends = builtin }

double-quote-literal ::= DOUBLE_QUOTE double-quote-chunk* DOUBLE_QUOTE
//double-quote-char =
//      %x20-21
//        ; %x22 = '"'
//    / %x23-5B
//        ; %x5C = "\"
//    / %x5D-7F
//    / valid-non-ascii
double-quote-char ::= pre_digit_printable_no_double_quote
                    | digit
                    | between_digit_upper_alpha
                    | upper_alpha
                    | between_upper_lower_alpha_no_backslash
                    | lower_alpha
                    | post_lower_alpha_printable
                    | VALID_NON_ASCII {extends = character elementType = character}


double-quote-chunk ::=
      interpolation
      // '\'    Beginning of escape sequence
    | BACKSLASH double-quote-escaped
    | double-quote-char {pin(".*")=BACKSLASH}

// N.B. this cannot inherit from Character because of the unicode-escape
double-quote-escaped ::= DOUBLE_QUOTE
                       | DOLLAR
                       | BACKSLASH
                       | SLASH
                       | b // 'b'    backspace       U+0008
                       | f // 'f'    form feed       U+000C
                       | n // 'n'    line feed       U+000A
                       | r // 'r'    carriage return U+000D
                       | t // 't'    tab             U+0009
                       | u unicode-escape

private single-quote-continue ::= interpolation single-quote-continue
                                | escaped-quote-pair single-quote-continue
                                | escaped-interpolation single-quote-continue
                                | SINGLE_QUOTE SINGLE_QUOTE
                                | single-quote-char single-quote-continue

escaped-quote-pair ::= SINGLE_QUOTE SINGLE_QUOTE SINGLE_QUOTE

escaped-interpolation ::= SINGLE_QUOTE SINGLE_QUOTE DOLLAR LEFT_BRACE

single-quote-char ::= all_ascii_printable
                    | TAB
                    | end-of-line
                    | VALID_NON_ASCII {extends = character elementType = character}

single-quote-literal ::= SINGLE_QUOTE SINGLE_QUOTE end-of-line single-quote-continue

interpolation ::= DOLLAR LEFT_BRACE complete-expression RIGHT_BRACE

unicode-escape ::= unbraced-escape | LEFT_BRACE braced-escape RIGHT_BRACE

braced-escape ::= ZERO* braced-codepoint

unbraced-escape ::= (digit | UA | a | UB | b | UC | c) hexdig hexdig hexdig
                  | (UD | d) (ZERO | ONE | TWO | THREE | FOUR | FIVE | SIX | SEVEN) hexdig hexdig
                  // %xD800-DFFF Surrogate pairs
                  | (UE | e) hexdig hexdig hexdig
                  | (UF | f) hexdig hexdig (digit | UA | a | UB | b | UC | c | UD | d)
                  // %xFFFE-FFFF Non-characters

braced-codepoint ::= (ONE | TWO | THREE | FOUR | FIVE | SIX | SEVEN | EIGHT | NINE
                    | UA | a | UB | b | UC | c | UD | d | UE | e | UF | f
                    | ONE ZERO) unicode-suffix
                    | unbraced-escape
                    | (hexdig | hexdig hexdig | hexdig hexdig hexdig)

unicode-suffix ::= (digit | UA | a | UB | b | UC | c | UD | d | UE | e) hexdig hexdig hexdig
                | (UF | f) hexdig hexdig (digit | UA | a | UB | b | UC | c | UD | d)

exponent ::= e (PLUS | MINUS)? digit*

numeric-double-literal ::= (PLUS | MINUS)? digit+ ( DOT digit+ exponent? | exponent)

minus-infinity-literal ::= MINUS infinity
plus-infinity-literal ::= infinity

primitive-expression ::=
    // "2.0"
      double-literal

    // "2"
    | natural-literal

    // "+2"
    | integer-literal

    // '"ABC"'
    | text-literal

    // "{ foo = 1      , bar = True }"
    // "{ foo : Integer, bar : Bool }"
    | LEFT_BRACE whsp (COMMA whsp)? record-type-or-literal whsp RIGHT_BRACE

    // "< Foo : Integer | Bar : Bool >"
    // "< Foo | Bar : Bool >"
    | LT whsp [ PIPE whsp ] union-type whsp GT

    // "[1, 2, 3]"
    | non-empty-list-literal

    // "x"
    // "x@2"
    | identifier

    // "( e )"
    | LEFT_PAREN complete-expression RIGHT_PAREN {pin(".*")="LEFT_BRACE|LEFT_PAREN" extends=operator-expression}

identifier ::= variable | builtin

variable ::= nonreserved-label (whsp AT whsp natural-literal)? { extends = identifier }

record-type-or-literal ::=
      empty-record-literal
    | non-empty-record-type-or-literal
    | empty-record-type

empty-record-literal ::= EQUALS
empty-record-type ::=

non-empty-record-type-or-literal ::= any-label-or-some whsp (non-empty-record-literal | non-empty-record-type)

non-empty-record-type ::= COLON whsp1 expression (whsp COMMA whsp record-type-entry)*
record-type-entry ::= any-label-or-some whsp COLON whsp1 expression

non-empty-record-literal ::= EQUALS whsp expression (whsp COMMA whsp record-literal-entry)*
record-literal-entry ::= any-label-or-some whsp EQUALS whsp expression

union-type ::=
      non-empty-union-type
    | empty-union-type

empty-union-type ::=

non-empty-union-type ::=
    union-type-entry (whsp PIPE whsp union-type-entry)*

//; x : Natural
//; x
union-type-entry ::= any-label-or-some (whsp COLON whsp1 expression)?

non-empty-list-literal ::=
    LEFT_SQUARE_BRACKET whsp (COMMA whsp)? expression whsp (COMMA whsp expression whsp)* RIGHT_SQUARE_BRACKET

// EXPRESSION tower of precedence

//  Nonempty-whitespace to disambiguate `env:VARIABLE` from type annotations
annotated-expression ::= operator-expression (whsp COLON whsp1 expression)?

// the FOLLOW set of let_binding. Note that that pinning changes the FOLLOW set to
// be more permissive, so we must manually write a rule to recover the FOLLOW set
// before pinning - "#auto" will not just work.
// N.B. Quick Documentation can be used to see the FIRST and FOLLOW sets.
private let_binding_recover ::= !(let | in)
//  "let x = e1"
let_binding ::= let whsp1 nonreserved-label whsp (COLON whsp1 expression whsp)? EQUALS whsp expression whsp {pin=1 recoverWhile=let_binding_recover}

//  "[] : t"
empty-list-literal ::=
    LEFT_SQUARE_BRACKET whsp (COMMA whsp)? RIGHT_SQUARE_BRACKET whsp COLON whsp1 application-primary {pin=COLON}

operator-expression ::= import-alt-expression
                      | or-expression
                      | plus-expression
                      | text-append-expression
                      | list-append-expression
                      | and-expression
                      | combine-expression
                      | prefer-expression
                      | combine-types-expression
                      | times-expression
                      | equal-expression
                      | not-equal-expression
                      | equivalent-expression
                      | application-primary
                      | import-expression
                      | completion-primary
                      | selector-primary
                      | primitive-expression

// Nonempty-whitespace to disambiguate `http://a/a?a`
import-alt-expression    ::= operator-expression whsp question whsp1 operator-expression                {extends = operator-expression}
or-expression            ::= operator-expression whsp double-pipe whsp operator-expression              {extends = operator-expression}
// Nonempty-whitespace to disambiguate `f +2`
plus-expression          ::= operator-expression whsp plus whsp1 operator-expression                    {extends = operator-expression}
text-append-expression   ::= operator-expression whsp double-plus whsp operator-expression              {extends = operator-expression}
list-append-expression   ::= operator-expression whsp hash-sign whsp operator-expression                {extends = operator-expression}
and-expression           ::= operator-expression whsp double-ampersand whsp operator-expression         {extends = operator-expression}
combine-expression       ::= operator-expression whsp combine whsp operator-expression                  {extends = operator-expression}
prefer-expression        ::= operator-expression whsp prefer whsp operator-expression                   {extends = operator-expression}
combine-types-expression ::= operator-expression whsp combine-types whsp operator-expression            {extends = operator-expression}
times-expression         ::= operator-expression whsp asterisk whsp operator-expression                 {extends = operator-expression}
equal-expression         ::= operator-expression whsp double-equals !EQUALS whsp operator-expression    {extends = operator-expression}
not-equal-expression     ::= operator-expression whsp not-equals whsp operator-expression               {extends = operator-expression}
equivalent-expression    ::= operator-expression whsp equivalent whsp operator-expression               {extends = operator-expression}

// Import expressions need to be separated by some whitespace, otherwise there
// would be ambiguity: `./ab` could be interpreted as "import the file `./ab`",
// or "apply the import `./a` to label `b`"
application-primary ::=
      first-application-expression application-expression* {extends = operator-expression}

// Possibly necessary so that the parser doesn't overly eagerly
// attempt
left application-expression ::= whsp1 import-expression {extends = operator-expression}

first-application-expression ::=
    // "merge e1 e2"
      merge whsp1 import-expression whsp1 import-expression

    // "Some e"
    | some whsp1 import-expression

    // "toMap e"
    | toMap whsp1 import-expression
    | import-expression {pin(".*")="merge|some|toMap" extends = operator-expression}


import-expression ::= import | completion-primary {extends = operator-expression}

local ::=
    parent-path
    | here-path
    | home-path
    // NOTE: Backtrack if parsing this alternative fails
    //
    // This is because the first character of this alternative will be "/", but
    // if the second character is "/" or "\" then this should have been parsed
    // as an operator instead of a path
    | absolute-path

parent-path ::= DOT DOT path  // Relative path
here-path ::= DOT path  // Relative path
home-path ::= TILDE path  // Home-anchored path
absolute-path ::= path  // Absolute path

path-character ::= EXCLAMATION
                | DOLLAR
                | PERCENT
                | AMPERSAND
                | SINGLE_QUOTE
                | ASTERISK
                | PLUS
                | MINUS
                | DOT
                | digit
                | COLON
                | SEMICOLON
                | EQUALS
                | AT
                | upper_alpha
                | CIRCUMFLEX
                | UNDERSCORE
                | GRAVE
                | lower_alpha
                | PIPE
                | TILDE {extends = character elementType = character}

quoted-path-character ::= pre_digit_printable_no_double_quote_no_slash
                        | digit
                        | between_digit_upper_alpha
                        | upper_alpha
                        | between_upper_lower_alpha
                        | lower_alpha
                        | post_lower_alpha_printable
                        | VALID_NON_ASCII {extends = character elementType = character}

unquoted-path-component ::= path-character+
quoted-path-component ::= quoted-path-character+

path-component ::= SLASH (unquoted-path-component | DOUBLE_QUOTE quoted-path-component DOUBLE_QUOTE)

path ::= path-component+

scheme ::= h t t p s?

http-raw ::= scheme COLON SLASH SLASH authority url-path (QUESTION query)? {pin = 4}

url-path ::= (path-component | SLASH segment)*

authority ::= (userinfo AT)? host (COLON port)?

userinfo ::= (unreserved | pct-encoded | sub-delims | COLON)*

host ::= ip-literal | ipv4address | domain

port ::= digit*

ip-literal ::= LEFT_SQUARE_BRACKET (ipv6address | ipvfuture) RIGHT_SQUARE_BRACKET

ipvfuture ::= v hexdig+ DOT (unreserved | sub-delims | COLON)+


//IPv6address =                            6( h16 ":" ) ls32
//            /                       "::" 5( h16 ":" ) ls32
//            / [ h16               ] "::" 4( h16 ":" ) ls32
//            / [ h16 *1( ":" h16 ) ] "::" 3( h16 ":" ) ls32
//            / [ h16 *2( ":" h16 ) ] "::" 2( h16 ":" ) ls32
//            / [ h16 *3( ":" h16 ) ] "::"    h16 ":"   ls32
//            / [ h16 *4( ":" h16 ) ] "::"              ls32
//            / [ h16 *5( ":" h16 ) ] "::"              h16
//            / [ h16 *6( ":" h16 ) ] "::"
ipv6address ::= three-h16-colon three-h16-colon ls32
            | double-colon three-h16-colon h16-colon h16-colon ls32
            | (h16)? double-colon three-h16-colon h16-colon ls32
            | (h16 colon-h16?)? double-colon three-h16-colon ls32
            | (h16 two-colon-h16?)? double-colon h16-colon h16-colon ls32
            | (h16 three-colon-h16?)? double-colon h16-colon ls32
            | (h16 four-colon-h16?)? double-colon ls32
            | (h16 five-colon-h16?)? double-colon h16
            | (h16 six-colon-h16?)? double-colon

// IPV6 Definition Helpers
h16 ::= hexdig hexdig hexdig hexdig | hexdig hexdig hexdig | hexdig hexdig | hexdig

private two-colon-h16 ::= colon-h16 colon-h16 | colon-h16
private three-colon-h16 ::= colon-h16 colon-h16 colon-h16 | two-colon-h16
private four-colon-h16 ::= colon-h16 colon-h16 colon-h16 colon-h16 | three-colon-h16
private five-colon-h16 ::= colon-h16 colon-h16 colon-h16 colon-h16 colon-h16 | four-colon-h16
private six-colon-h16 ::= colon-h16 colon-h16 colon-h16 colon-h16 colon-h16 colon-h16 | five-colon-h16

private double-colon ::= COLON COLON
private h16-colon ::= h16 COLON
private three-h16-colon ::= h16-colon h16-colon h16-colon
private colon-h16 ::= COLON h16

ls32 ::= h16 COLON h16 | ipv4address


ipv4address ::= dec-octet DOT dec-octet DOT dec-octet DOT dec-octet

dec-octet ::= TWO FIVE (ZERO | ONE | TWO | THREE | FOUR | FIVE)
            | TWO FOUR (ZERO | ONE | TWO | THREE | FOUR) digit
            | ONE digit digit
            | nonzero-digit digit
            | digit

domain ::= domainlabel (DOT domainlabel)* (DOT)?

domainlabel ::= alphanum+ (MINUS+ alphanum+)*

segment ::= pchar*

pchar ::= unreserved
        | pct-encoded
        | sub-delims
        | COLON
        | AT

query ::= (pchar | SLASH | QUESTION)*

pct-encoded ::= PERCENT hexdig hexdig

unreserved ::= alphanum | MINUS | DOT | UNDERSCORE | TILDE

// this is the RFC3986 sub-delims rule, without "(", ")" or ","
// see comments above the `http-raw` rule above
sub-delims ::= EXCLAMATION
            | DOLLAR
            | AMPERSAND
            | SINGLE_QUOTE
            | ASTERISK
            | PLUS
            | SEMICOLON
            | EQUALS

http ::= http-raw (whsp using whsp1 import-expression)?

env ::= e n v COLON (bash-environment-variable | DOUBLE_QUOTE posix-environment-variable DOUBLE_QUOTE)

bash-environment-variable ::= (alpha | UNDERSCORE) (alphanum | UNDERSCORE)*

posix-environment-variable ::= posix-environment-variable-character+

posix-environment-variable-character ::= BACKSLASH (DOUBLE_QUOTE | BACKSLASH | a | b | f | n | r | t | v)
                                       | pre_digit_printable_no_double_quote
                                       | digit
                                       | between_digit_upper_alpha_no_equals
                                       | upper_alpha
                                       | between_upper_lower_alpha_no_backslash
                                       | lower_alpha {extends = character elementType = character}

// "http://example.com"
// "./foo/bar"
// "env:FOO"
import ::= import-hashed (whsp as whsp1 (textid | location))? {pin = 1}

import-hashed ::= import-type (whsp1 hash)? {pin = 1}

hash ::= s h a TWO FIVE SIX COLON sixty-four-hex {pin=COLON} // "sha256:XX...XXX"

import-type ::= missing | local | http | env

completion-primary ::=
    selector-primary completion-expression? {extends = operator-expression}

left completion-expression ::= whsp complete whsp selector-primary {extends = operator-expression}

selector-primary ::= primitive-expression selector-expression* {extends = operator-expression}

left selector-expression::= whsp dot-op whsp selector {extends = operator-expression}

selector ::= any-label
            | labels
            | type-selector

type-selector ::= LEFT_PAREN whsp expression whsp RIGHT_PAREN

labels ::= LEFT_BRACE whsp (any-label-or-some whsp (COMMA whsp any-label-or-some whsp)*)? RIGHT_BRACE

private any-label ::= label

private any-label-or-some ::= label | some

double-literal ::= numeric-double-literal // "2.0"
                   // "-Infinity"
                 | minus-infinity-literal
                 // "Infinity"
                 | plus-infinity-literal
                 | nan

natural-literal ::=
    // Hexadecimal with "0x" prefix
      ZERO x hexdig+
    | digit+

integer-literal ::= (PLUS | MINUS) natural-literal

private text-literal ::= double-quote-literal
                       | single-quote-literal

end-of-line ::= NEW_LINE
             | CARRIAGE_RETURN NEW_LINE

block-comment ::= LEFT_BRACE MINUS block-comment-continue {pin = 2}

block-comment-char ::= all_ascii_printable
                    | VALID_NON_ASCII
                    | TAB
                    | end-of-line {extends = character elementType = character}

// Once we have matched a block comment, we must pick the block comment rule. This is because
// the ABNF does not specify that backtracking is to be done in the case that the block-comment
// rule fails.
// Consider, for example, {- {- -}. If we allow failure in the second branch, then this gets parsed
// using the block-comment-char rule, which will successfully parse this as the string " {- " within
// a block comment, instead of an unclosed block comment containing the nested block comment, which is
// the correct parse.
// The reason we need to explicitly state the pin is because some prefix for the second rule can also
// be matched by the third rule.
private block-comment-continue ::= MINUS RIGHT_BRACE
                        | block-comment block-comment-continue
                        | block-comment-char block-comment-continue {pin(".*")=block-comment}

not-end-of-line ::= all_ascii_printable
                  | VALID_NON_ASCII
                  | TAB

line-comment ::= MINUS MINUS not-end-of-line* end-of-line

private whitespace-chunk ::= SPACE
                   | TAB
                   | end-of-line
                   | line-comment
                   | block-comment

private whsp ::= whitespace-chunk*

private whsp1 ::= whitespace-chunk+


// compact definition of sixty-four hex digits
private sixty-four-hex ::= sixteen-hex sixteen-hex sixteen-hex sixteen-hex {pin = 1}
private sixteen-hex ::= four-hex four-hex four-hex four-hex {pin = 1}
private four-hex ::= hexdig hexdig hexdig hexdig {pin = 1}

hexdig ::= digit | UA | a | UB | b | UC | c | UD | d | UE | e | UF | f

// MAJOR CHARACTER CLASSES

// x41-x5a, x61-x71
private alpha ::= upper_alpha | lower_alpha

// x41 - x5A
private upper_alpha ::= UA | UB | UC | UD | UE | UF | UI | UK
                    | UL | UM | UN | UO | US | UT
                    | UZ | UALPHA
// x61 - x7a
private lower_alpha ::= a | b | c | d | e | f | g | h | i | k | l | m | n
                     | o | p | r | s | t | u | v | w | x | y | LALPHA

// x30-x39, x41-x5a, x61-x7a
private alphanum ::= alpha | digit

// x30 - x39
digit ::= ZERO | nonzero-digit
private nonzero-digit ::= ONE | TWO | THREE | FOUR | FIVE | SIX | SEVEN | EIGHT | NINE
// x20 - x2f
private pre_digit_printable ::= pre_digit_printable_no_double_quote | DOUBLE_QUOTE
private pre_digit_printable_no_double_quote ::= pre_digit_printable_no_double_quote_no_slash | SLASH
private pre_digit_printable_no_double_quote_no_slash ::= SPACE
                                                      | EXCLAMATION
                                                      | HASH_SIGN
                                                      | DOLLAR
                                                      | PERCENT
                                                      | AMPERSAND
                                                      | SINGLE_QUOTE
                                                      | LEFT_PAREN
                                                      | RIGHT_PAREN
                                                      | ASTERISK
                                                      | PLUS
                                                      | COMMA
                                                      | MINUS
                                                      | DOT

// x3a - x40
private between_digit_upper_alpha ::= between_digit_upper_alpha_no_equals | EQUALS
private between_digit_upper_alpha_no_equals ::= COLON | SEMICOLON | LT | GT | QUESTION | AT

// x5b - x60
private between_upper_lower_alpha ::= between_upper_lower_alpha_no_grave | GRAVE
private between_upper_lower_alpha_no_grave ::= LEFT_SQUARE_BRACKET | BACKSLASH | RIGHT_SQUARE_BRACKET | CIRCUMFLEX | UNDERSCORE
private between_upper_lower_alpha_no_backslash ::= LEFT_SQUARE_BRACKET | RIGHT_SQUARE_BRACKET | CIRCUMFLEX | UNDERSCORE | GRAVE

private post_lower_alpha_printable ::= LEFT_BRACE | PIPE | RIGHT_BRACE | TILDE | DELETE

private all_ascii_printable ::= pre_digit_printable
                            | digit
                            | between_digit_upper_alpha
                            | upper_alpha
                            | between_upper_lower_alpha
                            | lower_alpha
                            | post_lower_alpha_printable
